/*
 * Top-level build file for the Jagger project.
 *
 * A uniform Java configuration is defined for all subprojects. All
 * inter-project dependencies are defined in this top-level build, while
 * external dependencies are defined in the individual projects.
 */

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'eclipse'

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    group = 'com.scottkrulcik'

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation 'com.google.guava:guava:22.0'
        testCompile "junit:junit:4.12"
    }

}

project(':agnostic') {
    apply plugin: 'java-library'
}

project(':processor') {
    apply plugin: 'java-library'

    dependencies {
        api project(':agnostic')
    }
}

project(':examples') {
    dependencies {
        compile project(':agnostic'), project(':processor')
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
